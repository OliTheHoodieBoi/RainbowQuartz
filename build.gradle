import org.jetbrains.kotlin.gradle.dsl.ExplicitApiMode

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'jacoco'
}

group = 'com.olillin.rainbowquartz'
version = '0.1.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url 'https://libraries.minecraft.net/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    implementation 'me.lucko:commodore:2.2'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.16.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.20:3.78.0'
    testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.12'
}

tasks.build.dependsOn tasks.shadowJar

shadowJar {
    manifest {
        archiveClassifier = null
    }

    dependencies {
        exclude(dependency('com.mojang:brigadier'))
        exclude(dependency('net.kyori:adventure-text-serializer-legacy'))
        exclude('kotlin/')
    }

    relocate('me.lucko.commodore', 'com.olillin.rainbowquartz.commodore')
}

def targetJavaVersion = 17
kotlin {
    jvmToolchain(targetJavaVersion)
    explicitApi = ExplicitApiMode.Strict
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}